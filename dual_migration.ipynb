{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "be620a16-b529-4e25-b1a7-c4613e5e312b",
   "metadata": {},
   "source": [
    "Paper: City formation by dual migration of firms and workers  \n",
    "Author: Kensuke Ohtake"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "15126ebc-8b3a-4be9-ba62-dc05274b6789",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random\n",
    "using Distributions\n",
    "using Plots\n",
    "using Dates\n",
    "using Format\n",
    "using DataFrames\n",
    "using CSV\n",
    "\n",
    "# simulation function\n",
    "function dualmigration_simulation(pls)\n",
    "\n",
    "    # random seed\n",
    "    SD = Dates.millisecond(now()) # current time\n",
    "    Random.seed!(SD) # set seed\n",
    "    \n",
    "    # control parameters \n",
    "    sig = pls[1] # elasticity of substitution of manufactured goods\n",
    "    tau = pls[2] # manufacturing transport cost parameter\n",
    "\n",
    "    # function of shorter distance on S\n",
    "    function dist(z)\n",
    "        minimum([abs(z), 2.0 * π - abs(z)])\n",
    "    end\n",
    "\n",
    "    # hyper parameters\n",
    "    R = 256 # number of space nodes\n",
    "    dx = 2.0 * π / convert(Float64, R) # size of space element\n",
    "    dt = 0.01 # size of time step\n",
    "    mu = 0.6 # manufacturing share\n",
    "    eps = 1e-10 # acceptable error\n",
    "    v_m = 1.0 # realwage sensitivity of workers\n",
    "    v_n = 1.0 # realprofit sensitivity of firms\n",
    "\n",
    "    # x-coordinate\n",
    "    x = zeros(Float64, R, 1)\n",
    "    for i in 1:R\n",
    "        x[i] = -π + (i - 1) * dx\n",
    "    end\n",
    "    \n",
    "    # agricultural workers (density)\n",
    "    ph = ones(Float64, (R, 1)) / (2.0 * π)\n",
    "    \n",
    "    # transport matrix for manufacturing\n",
    "    Ex = zeros(Float64, (R, R))\n",
    "    for i in 1:R\n",
    "        for j in 1:R\n",
    "            Ex[i,j] = exp(- tau * (sig - 1.0) * dist(x[i]-x[j]))\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # integral operator for instantaneous equilibrium\n",
    "    function IntegOperat(nominalwage_power, agr_workers, workers, firms)# Instantaneous equilibrium operator\n",
    "        \n",
    "        #= \n",
    "        Inputs must be array (R, 1)\n",
    "        nominalwage_power: manufacturing nominal wage raised to the power of sigma\n",
    "        agr_workers: density of agricultural workers phi(x)\n",
    "        workers: density of manufactirng workers m(x)\n",
    "        firms: density of manufacturing firms n(t, x)\n",
    "        =#\n",
    "\n",
    "        # nominal income (but sigma omitted)\n",
    "        nominalincome = (1.0 - mu) * agr_workers + mu * (nominalwage_power .^ (1.0 / sig)) .* workers\n",
    "\n",
    "        # price index raised to the power of 1-sigma (but dx and sigma omitted)\n",
    "        priceindex_power = Ex * (firms .* (nominalwage_power .^ ((1.0 - sig) / sig)))\n",
    "\n",
    "        # output of integral operator (but dx omitted) \n",
    "        output = (firms ./ workers) .* (Ex * (nominalincome ./ priceindex_power))\n",
    "        \n",
    "        return output\n",
    "    end\n",
    "    \n",
    "    # function to obtain right-hand side of evolution equation\n",
    "    function RHS(pot_nominalwage_power, agr_workers, workers, firms)\n",
    "        \n",
    "        #=\n",
    "        Inputs must be array (R, 1)\n",
    "        pot_nominalwage_power: potential solution for instantaneous equilibrium equation\n",
    "        (manufacturing nominalwage reised to the power of sigma)\n",
    "        =#\n",
    "        \n",
    "        # solve fixed point problem for instantaneous equilibrium by iterative method\n",
    "        nominalwage_power = copy(pot_nominalwage_power) # initial value for iteration\n",
    "        while true\n",
    "            \n",
    "            old_nominalwage_power = copy(nominalwage_power) # store the previous value\n",
    "            nominalwage_power = IntegOperat(old_nominalwage_power, agr_workers, workers, firms) # update the value\n",
    "            \n",
    "            if maximum(abs.(nominalwage_power - old_nominalwage_power)) < eps # stop condition (max norm < epsilon)\n",
    "                break\n",
    "            end\n",
    "            \n",
    "        end\n",
    "\n",
    "        # other equilibrium variables\n",
    "        priceindex = (sig * dx * Ex * (firms .* (nominalwage_power .^ ((1.0 - sig) / sig)))) .^ (1.0 / (1.0 - sig)) # manufacturing price index\n",
    "        realwage = (nominalwage_power .^ (1.0 / sig)) .* (priceindex .^ (- mu)) # manufacturing real wage\n",
    "        realprofit = (mu / sig) * (workers ./ firms) .* realwage # manufacturing real profit\n",
    "\n",
    "        # # verification that realwage is a column vector\n",
    "        # if size(realwage) != (R, 1)\n",
    "        #     error(\"realwage shape error!\")\n",
    "        # end\n",
    "        \n",
    "        # right-hand side of evolution equations\n",
    "        rhs_workers = v_m * ((realwage .- (transpose(realwage) * workers * dx)) .* workers)\n",
    "        rhs_firms = v_n * ((realprofit .- (transpose(realprofit) * firms * dx)) .* firms)\n",
    "        \n",
    "        return rhs_workers, rhs_firms, nominalwage_power\n",
    "    end\n",
    "    \n",
    "    #=\n",
    "    simulation of the evolution equations\n",
    "    =#\n",
    "\n",
    "    # initial values of manufacturing workers\n",
    "    delta_m = rand(Uniform(-0.0005, 0.0005), (R, 1)) # perturbation generated by random numbers\n",
    "    delta_m = delta_m .- (sum(delta_m) * dx) / (2.0 * π) # total integral equals to 0\n",
    "    m = (ones(Float64, (R, 1)) / (2.0 * π)) + delta_m # manufacturing workers: homogeneous state + perturbation\n",
    "\n",
    "    # initial values of firms\n",
    "    delta_n = rand(Uniform(-0.0005, 0.0005), (R, 1)) # perturbation generated by random numbers\n",
    "    delta_n = delta_n .- (sum(delta_n) * dx) / (2.0 * π) # total equals to 0\n",
    "    n = (ones(Float64, (R, 1)) / (2.0 * π)) + delta_n # firms: homogeneous state + perturbation\n",
    "\n",
    "    # verify difference in initial values of m and n\n",
    "    if n == m\n",
    "        error(\"same initial value !\")\n",
    "    end\n",
    "\n",
    "    # check non-negativity\n",
    "    if sum(sign.(m)) != R || sum(sign.(n)) != R\n",
    "        error(\"non-negativity error!\")\n",
    "    end\n",
    "\n",
    "    # check conservation\n",
    "    if 1.0 - 1e-10 > sum(m) * dx || 1.0 - 1e-10 > sum(n) * dx # The total integral is too small\n",
    "        error(\"conservation error!\")\n",
    "    elseif sum(m) * dx > 1.0 + 1e-10 || sum(n) * dx > 1.0 + 1e-10 # The total integral is too large\n",
    "        error(\"conservation error!\")\n",
    "    end\n",
    "\n",
    "    # save initial values\n",
    "    ini_m = copy(m)\n",
    "    println(\"initial total manufacturing workers is \", sum(ini_m) * dx)\n",
    "    ini_n = copy(n)\n",
    "    println(\"initial total firms is \", sum(ini_n) * dx)\n",
    "    \n",
    "    t = 0 # initial time index\n",
    "    W = ones(Float64, (R, 1)) # initial potential solution to instantaneous equilibrium equation\n",
    "\n",
    "    while true\n",
    "\n",
    "        # store the previous values\n",
    "        old_m = copy(m) # manufacturing workers\n",
    "        old_n = copy(n) # manufacturing firms\n",
    "        old_W = copy(W) # instantaneous equilibrium\n",
    "        \n",
    "        # 1-st order Runge Kutta method (Euler method) for the replicator equation \n",
    "        k1m, k1n, W = RHS(old_W, ph, m, n) # obtain right-hand sides of evolution equations and instantaneous equilibrium\n",
    "        dm = dt * k1m # update size of m\n",
    "        dn = dt * k1n # update size of n\n",
    "        m = old_m + dm # update m by Euler method\n",
    "        n = old_n + dn # update n by Euler method\n",
    "\n",
    "        # check non-negativity\n",
    "        if sum(sign.(m)) != R || sum(sign.(n)) != R\n",
    "            println(\"non-negativity error!\")\n",
    "            break\n",
    "        end\n",
    "        \n",
    "        # check conservation\n",
    "        if 1.0 - 1e-10 > sum(m) * dx || 1.0 - 1e-10 > sum(n) * dx # The total integral is too small\n",
    "            println(\"conservation error!\")\n",
    "            break\n",
    "        elseif sum(m) * dx > 1.0 + 1e-10 || sum(n) * dx > 1.0 + 1e-10 # The total integral is too large\n",
    "            println(\"conservation error!\")\n",
    "            break\n",
    "        end\n",
    "\n",
    "        # stop condition (max norm < epsilon)\n",
    "        if maximum(abs.(dm)) < eps && maximum(abs.(dn)) < eps\n",
    "            break\n",
    "        end\n",
    "    \n",
    "        t += 1 # update time index\n",
    "    \n",
    "    end\n",
    "\n",
    "    #=\n",
    "    obtain stationary solution\n",
    "    =#\n",
    "    \n",
    "    # compute instantaneous equilibrium for the final m and n\n",
    "    stationary_W = copy(W)\n",
    "    while true\n",
    "        old_stationary_W = copy(stationary_W) # store the previpus stationary W\n",
    "        stationary_W = IntegOperat(old_stationary_W, ph, m, n) # update stationary W\n",
    "        if maximum(abs.(stationary_W - old_stationary_W)) < eps # stop condition (max norm < epsilon)\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # stationary states\n",
    "    w = stationary_W .^ (1.0 / sig) # nominal wage\n",
    "    G = (sig * dx * Ex * (n .* (w .^ (1.0 - sig)))) .^ (1.0 / (1.0 - sig)) # price index\n",
    "    omega = w .* (G .^ (-mu)) # real wage\n",
    "    eta = (mu / sig) * (m ./ n) .* omega # real profit\n",
    "\n",
    "    # show total density\n",
    "    println(\"Final total manufacturing workers is \", sum(m) * dx)\n",
    "    println(\"Final total manufacturing firms is \", sum(n) * dx)\n",
    "\n",
    "    #=\n",
    "    output results\n",
    "    =#\n",
    "    \n",
    "    # plot\n",
    "    DateTime = Dates.format(now(), \"yyyymmddHHMMSS\")# get current time\n",
    "    plt_x = append!(reshape(x, R), π) # periodic condition for x-coordinate\n",
    "    plt_ini_m = append!(reshape(ini_m, R), ini_m[1]) # periodic condition for initial m\n",
    "    plt_ini_n = append!(reshape(ini_n, R), ini_n[1]) # periodic condition for initial n\n",
    "    plt_m = append!(reshape(m, R), m[1]) # periodic condition for m\n",
    "    plt_n = append!(reshape(n, R), n[1]) # periodic condition for n\n",
    "    plt_w = append!(reshape(w, R), w[1]) # periodic condition for w\n",
    "    plt_G = append!(reshape(G, R), G[1]) # periodic condition for G\n",
    "    plt_omega = append!(reshape(omega, R), omega[1]) # periodic condition for omega\n",
    "    plt_eta = append!(reshape(eta, R), eta[1]) # periodic condition for eta\n",
    "\n",
    "    filename_initial = format(\"{}-initial_tau{}sigma{}mu{}R{}t{}sd{}\", DateTime, tau, sig, mu, R, t, SD)\n",
    "    newfilename_initial = replace(filename_initial, \".\"=>\"p\")\n",
    "    filename_m = format(\"{}-workers_tau{}sigma{}mu{}R{}t{}sd{}\", DateTime, tau, sig, mu, R, t, SD)\n",
    "    newfilename_m = replace(filename_m, \".\"=>\"p\")\n",
    "    filename_n = format(\"{}-firms_tau{}sigma{}mu{}R{}t{}sd{}\", DateTime, tau, sig, mu, R, t, SD)\n",
    "    newfilename_n = replace(filename_n, \".\"=>\"p\")  \n",
    "    filename_w = format(\"{}-nominalwage_tau{}sigma{}mu{}R{}t{}sd{}\", DateTime, tau, sig, mu, R, t, SD)\n",
    "    newfilename_w = replace(filename_w, \".\"=>\"p\")\n",
    "    filename_G = format(\"{}-priceindex_tau{}sigma{}mu{}R{}t{}sd{}\", DateTime, tau, sig, mu, R, t, SD)\n",
    "    newfilename_G = replace(filename_G, \".\"=>\"p\") \n",
    "    filename_omega = format(\"{}-realwage_tau{}sigma{}mu{}R{}t{}sd{}\", DateTime, tau, sig, mu, R, t, SD)\n",
    "    newfilename_omega = replace(filename_omega, \".\"=>\"p\")\n",
    "    filename_eta = format(\"{}-realprofit_tau{}sigma{}mu{}R{}t{}sd{}\", DateTime, tau, sig, mu, R, t, SD)\n",
    "    newfilename_eta = replace(filename_eta, \".\"=>\"p\")\n",
    "\n",
    "    plot(plt_x, plt_ini_m, title=\"initial values\", label=\"initial worlers\", dpi=300)\n",
    "    plot!(plt_x, plt_ini_n, title=\"initial values\", label=\"initial firms\", dpi=300)\n",
    "    savefig(newfilename_initial * \".png\")\n",
    "    plot(plt_x, plt_m, title=\"workers\", label=\"\", dpi=300)\n",
    "    savefig(newfilename_m * \".png\")\n",
    "    plot(plt_x, plt_n, title=\"firms\", label=\"\", dpi=300)\n",
    "    savefig(newfilename_n * \".png\")\n",
    "    plot(plt_x, plt_w, title=\"nominal wage\", label=\"\", dpi=300)\n",
    "    savefig(newfilename_w * \".png\")\n",
    "    plot(plt_x, plt_G, title=\"price index\", label=\"\", dpi=300)\n",
    "    savefig(newfilename_G * \".png\")\n",
    "    plot(plt_x, plt_omega, title=\"realwage\", label=\"\", dpi=300)\n",
    "    savefig(newfilename_omega * \".png\")\n",
    "    plot(plt_x, plt_eta, title=\"realprofit\", label=\"\", dpi=300)\n",
    "    savefig(newfilename_eta * \".png\")\n",
    "    \n",
    "    # save data as csv file\n",
    "    df = DataFrame(coordinates = plt_x,\n",
    "                   initial_pop_share_n = plt_ini_n,\n",
    "                   initial_pop_share_m = plt_ini_m,\n",
    "                   final_pop_share_n = plt_n,\n",
    "                   final_pop_share_m = plt_m,\n",
    "                   manuf_nominal_wage = plt_w,\n",
    "                   manuf_price_index = plt_G,\n",
    "                   firms_real_profit = plt_eta,\n",
    "                   manuf_real_wage_M = plt_omega) # make dataframe\n",
    "    filename = format(\"{}_tau{}sigma{}mu{}R{}t{}sd{}\", DateTime, tau, sig, mu, R, t, SD)\n",
    "    newfilename = replace(filename, \".\"=>\"p\")\n",
    "    CSV.write(newfilename * \".csv\", df) # write into csv\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "556c5305-4cc2-49f0-988c-6a5199417bea",
   "metadata": {},
   "outputs": [],
   "source": [
    "# run the simulation\n",
    "for tau in [0.5, 0.7, 1.1, 1.7, 2.0, 2.6]\n",
    "    params = [5.0, tau] # set parameters [sigma, tau]\n",
    "    for itr in 1:5\n",
    "        dualmigration_simulation(params)\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1410808b-98cd-4763-ab69-c6703cfcb3ca",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.4",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
